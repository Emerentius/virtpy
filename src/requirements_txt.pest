// NOTE: does not implement ALL functionality of requirements.txt
// May have to expand in the future.
// Full format specs: https://pip.pypa.io/en/stable/reference/pip_install/#requirements-file-format

requirements = { (requirement ~ (NEWLINE ~ requirement)*)? ~ NEWLINE? ~ &EOI }
requirement = { ident ~ "==" ~ version ~ system_condition ~ hash* }

// distributions may contain hyphens ("-"), but module names
// don't
ident = @{ (ASCII_ALPHA | "_") ~ (ASCII_ALPHANUMERIC | "_" | "-")* }
hash = ${ "--hash=" ~ sha_hash }
sha_hash = { "sha256:" ~ ASCII_HEX_DIGIT{64} }

system_condition = { (";" ~ "sys_platform" ~ ("==" | "!=") ~ system)? }
system = @{ "\"" ~ ASCII_ALPHANUMERIC+ ~ "\"" }

// https://www.python.org/dev/peps/pep-0440/#public-version-identifiers
version = @{ epoch? ~ release ~ pre_release? ~ post_release? ~ dev_release?}
epoch = { number ~ "!" }
release = { number ~ dot_number* }
pre_release = { ("a" | "b" | "rc" ) ~ number }
post_release = { dot_number }
dev_release = { dot_number }
dot_number = { "." ~ number }
//version = @{ number ~ "." ~ number ~ "." ~ number }

number = { ASCII_DIGIT+ }

WHITESPACE = _{ " " | "\t" | ("\\" ~ NEWLINE) }
